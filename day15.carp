(def in (IO.read-file "../input.13.txt"))
(def the-map (Array.pop-back (String.lines &in)))

(defn at [map coord]
  (let [x @(Pair.a coord)
        y @(Pair.b coord)]
    (do
      (if (and (and (<= 0 y) (< y (Array.length map)))
               (and (<= 0 x) (< x (String.length (Array.nth map y)))))
        (String.char-at (Array.nth map y) x)
        \#))))

(defn floor? [map coord]
  (not (= \# (at map coord))))

(defn neighbours [coord]
  (let [x @(Pair.a coord)
        y @(Pair.b coord)]
    [(Pair.init x (dec y))
     (Pair.init (dec x) y)
     (Pair.init (inc x) y)
     (Pair.init x (inc y))]))

(defn compare-coord [c1 c2]
  (if (= (Pair.b c1) (Pair.b c2))
    (> (Pair.a c1) (Pair.a c2))
    (> (Pair.b c1) (Pair.b c2))))

;; TODO move to core
;; TODO could be reduce instead
(defn concat-map [f xs]
  (Array.concat
   &(Array.copy-map f xs)))

(defn bfs [map free? found? coords]
  (let [visited (Set.create)
        nexts (Array.sorted-by coords &compare-coord)
        dist 0
        found (Pair.init -1 -1)]
    (do
      (while true
        (do
          (when (Array.empty? &nexts)
            (do
              (set! dist -1)
              (break)))
          (let [founds (Array.filter found? @&nexts)]
            (unless (Array.empty? &founds)
              (do
                (set! found (Array.first &founds))
                (break))))
          (set! visited (Array.reduce &Set.put
                                      visited
                                      &nexts))
          (set! dist (inc dist))
          (let [candidates (concat-map
                            &(fn [coord] (Array.filter
                                          free?
                                          (neighbours coord)))
                            &(Set.to-array &visited))
                visited-ref &visited
                new-nexts (Array.sort-by
                           (Array.filter
                            &(fn [coord] (not (Set.contains? visited-ref coord)))
                            candidates)
                           &compare-coord)]
            (set! nexts new-nexts))))
      (Pair.init dist found))))

(defn neighbouring? [c1 c2]
  (let [x1 @(Pair.a c1)
        y1 @(Pair.b c1)
        x2 @(Pair.a c2)
        y2 @(Pair.b c2)]
    (= 1
       (+ (Int.abs (- x1 x2))
          (Int.abs (- y1 y2))))))

(deftype Unit [type Char
               pos (Pair Int Int)])

(defn parse-units [map]
  (let [units []]
    (do
      (for [y 0 (Array.length map)]
        (let [line (Array.nth map y)]
          (for [x 0 (String.length line)]
            (let [c (String.char-at line x)]
              (when (or (= c \E)
                        (= c \G))
                (do
                  (String.string-set! line x \.)
                  (set! units (Array.push-back units (Unit.init c (Pair.init x y))))))))))
      units)))

(defn find-move [map units unit]
  (let [unit-positions (Array.copy-map &(fn [u] @(Unit.pos u)) units)
        obstructions (Set.from-array &unit-positions)
        obstructions-ref &obstructions
        free? (fn [c] (and (floor? map c)
                           (not (Set.contains? obstructions-ref c))))
        free?ref &free?
        targets (concat-map &(fn [u]
                               (if (= (Unit.type u) (Unit.type unit))
                                 []
                                 (neighbours (Unit.pos u))))
                            units)
        possible-next-move? (fn [c] (and (~free?ref c)
                                         (neighbouring? c (Unit.pos unit))))]
    (if (Array.any? &(fn [c] (= c (Unit.pos unit)))
                    &targets)
      @unit ;; no need to move
      (let [free-targets (Array.filter free?ref targets)
            goal (bfs map &free? &possible-next-move? &free-targets)
            goal-dist @(Pair.a &goal)
            goal-coord @(Pair.b &goal)]
        (if (< goal-dist 0)
          @unit ;; can't move
          (Unit.set-pos @unit goal-coord))))))

(defn compare-unit [u1 u2]
  (compare-coord (Unit.pos u1) (Unit.pos u2)))

(defn tick [map units]
  (do
    (set! units (Array.sort-by units &compare-unit))
    (for [i 0 (Array.length &units)]
      (let [new (find-move map &units (Array.nth &units i))]
        (set! units (Array.aset units i new))))
    units))

;; TODO to core?
(defn zero []
  \?)

(defn draw [map units]
  (let [by-pos (Map.from-array
                &(Array.copy-map
                  &(fn [u] (Pair.init-from-refs (Unit.pos u) (Unit.type u)))
                  units))]
    (for [y 0 (Array.length map)]
      (let [line (Array.nth map y)]
        (do
          (for [x 0 (String.length line)]
            (let [c (Pair.init x y)]
              (if (Map.contains? &by-pos &c)
                (print* (Map.get &by-pos &c))
                (print* (String.char-at line x)))))
          (println* ""))))))

(def test-map
  [@"#########"
   @"#G..G..G#"
   @"#.......#"
   @"#.......#"
   @"#G..E..G#"
   @"#.......#"
   @"#.......#"
   @"#G..G..G#"
   @"#########"])

(def test-map-ref &test-map)

(defn main []
  (let [units (parse-units test-map-ref)]
    (for [_ 0 4]
      (do
        (println* &units)
        (draw test-map-ref &units)
        (set! units (tick test-map-ref units))))))
