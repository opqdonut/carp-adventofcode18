(def in-lines (Array.pop-back (String.lines &(IO.read-file "../input.18.txt"))))

(defn parse [ls]
  (let [ret {}]
    (do
      (for [y 0 (Array.length ls)]
        (let [line (Array.nth ls y)]
          (for [x 0 (String.length line)]
            (set! ret (Map.put ret &(Pair.init x y) &(String.char-at line x))))))
      ret)))

(def the-map (parse &in-lines))

(def test-map
  (parse
   &[@".#.#...|#."
     @".....#|##|"
     @".|..|...#."
     @"..|#.....#"
     @"#.#|||#|#|"
     @"...#.||..."
     @".|....|..."
     @"||...#|.#|"
     @"|.||||..|."
     @"...#.|..|."]))

(defn zero []
  \x)

(defn neighbours [map coord]
  (let [x @(Pair.a coord)
        y @(Pair.b coord)]
    [(Map.get map &(Pair.init (dec x) (dec y)))
     (Map.get map &(Pair.init x (dec y)))
     (Map.get map &(Pair.init (inc x) (dec y)))
     (Map.get map &(Pair.init (dec x) y))
     (Map.get map &(Pair.init (inc x) y))
     (Map.get map &(Pair.init (dec x) (inc y)))
     (Map.get map &(Pair.init x (inc y)))
     (Map.get map &(Pair.init (inc x) (inc y)))]))

(defn count-neighbours [map coord type]
  (Array.element-count &(neighbours map coord)
                       &type))

(defn evolve-point [map coord]
  (let [current (Map.get map coord)
        trees (count-neighbours map coord \|)
        lumber (count-neighbours map coord \#)]
    (case current
      \. (if (<= 3 trees) \| \.)
      \| (if (<= 3 lumber) \# \|)
      \# (if (and (<= 1 lumber)
                  (<= 1 trees))
           \#
           \.)
      current)))

(defn evolve [map size]
  (let [new-map {}]
    (do
      (for [x 0 size]
        (for [y 0 size]
          (let [coord (Pair.init x y)]
            (set! new-map (Map.put new-map &coord &(evolve-point map &coord))))))
      new-map)))

(defn draw [map size]
  (for [y 0 size]
    (do
      (for [x 0 size]
        (let [coord (Pair.init x y)]
          (print* (Map.get map &coord))))
      (println* ""))))

(defn part1 [steps]
  (let [map @&the-map
        siz 50]
    (do
      (for [_ 0 steps]
        (do
          (set! map (evolve &map siz))
          ;;(draw &map siz)
          ;;(println* "")
          ))
      (let [resources (Map.vals &map)
            lumber (Array.element-count &resources &\#)
            trees (Array.element-count &resources &\|)]
        (println* (* lumber trees))))))

;; TODO to core
(defn = [m1 m2]
  (and (= (Map.length m1) (Map.length m2))
       (Map.kv-reduce (fn [b k v] (and b (= v &(Map.get m2 k))))
                      true
                      m1)))

(defn find-cycle [init siz]
  (let [t-hare 0
        t-tortoise 0
        hare @init
        tortoise @init
        go-tortoise false]
    (do
      (for [_ 0 1000]
        (do
          (when go-tortoise
            (do
              (set! tortoise (evolve &tortoise siz))
              (set! t-tortoise (Int.inc t-tortoise))))
          (set! go-tortoise (Bool.not go-tortoise))
          (set! hare (evolve &hare siz))
          (set! t-hare (Int.inc t-hare))
          ;;(println* (Set.length &hare))
          ;;(println* &hare)
          (when (= &tortoise &hare)
            (break))))
      (Pair.init t-tortoise t-hare))))

(defn part2 []
  (let [target 1000000000l
        cycle (find-cycle &the-map 50)
        start (Long.from-int @(Pair.a &cycle))
        end (Long.from-int @(Pair.b &cycle))
        len (- end start)
        to-go (Long.mod (- target start) len)
        total (Long.to-int (+ start to-go))]
    (do
      ;;(println* &cycle)
      ;;(println* total)
      (part1 total))))

(defn main []
  (do
    (part1 20)
    (part2)
    ))
